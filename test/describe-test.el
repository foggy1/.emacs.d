(ert-deftest describe-commit ()
  (with-temp-dir path
    (init)
    (commit-change "a" "abc")
    (run "git" "tag" "-a" "v1" "-m" "v1")
    (let* ((repo (libgit-repository-open path))
           (commit (libgit-revparse-single repo "HEAD"))
           (id (libgit-object-id commit)))
      (should (string= (libgit-describe-commit commit) "v1"))
      (should (string= (libgit-describe-commit commit '((always-use-long-format . t)))
                       (concat "v1-0-g" (substring id 0 7)))))
    (commit-change "b" "abc")
    (let* ((repo (libgit-repository-open path))
           (commit (libgit-revparse-single repo "HEAD"))
           (id (libgit-object-id commit)))
      (should (string= (libgit-describe-commit commit)
                       (concat "v1-1-g" (substring id 0 7))))
      (should (string= (libgit-describe-commit commit '((strategy . all))) "heads/master")))))

(ert-deftest describe-workdir ()
  (with-temp-dir path
    (init)
    (commit-change "a" "abc")
    (run "git" "tag" "-a" "v1" "-m" "v1")
    (let* ((repo (libgit-repository-open path))
           (id (libgit-reference-name-to-id repo "HEAD")))
      (should (string= (libgit-describe-workdir repo) "v1"))
      (should (string= (libgit-describe-workdir repo '((always-use-long-format . t)))
                       (concat "v1-0-g" (substring id 0 7)))))
    (write "a" "xyz")
    (let* ((repo (libgit-repository-open path))
           (id (libgit-reference-name-to-id repo "HEAD")))
      (should (string= (libgit-describe-workdir repo) "v1"))
      (should (string= (libgit-describe-workdir repo '((always-use-long-format . t)))
                       (concat "v1-0-g" (substring id 0 7))))
      (should (string= (libgit-describe-workdir repo '((dirty-suffix . "*"))) "v1*"))
      (should (string= (libgit-describe-workdir repo '((always-use-long-format . t)
                                                       (dirty-suffix . "---")))
                       (concat "v1-0-g" (substring id 0 7) "---"))))))
