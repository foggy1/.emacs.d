(ert-deftest index ()
  (with-temp-dir path
    (init)
    (commit-change "file1" "content")
    (commit-change "file2" "more content")
    (write "file2" "changes")
    (write "file3" "more changes")
    (write "file4" "even more changes")
    (add "file2" "file3")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 3 (libgit-index-entrycount index)))
      (let* ((i1 (libgit-index-get-byindex index 0))
             (i2 (libgit-index-get-byindex index 1))
             (i3 (libgit-index-get-byindex index 2))
             (head (libgit-reference-name-to-id repo "HEAD"))
             (commit (libgit-commit-lookup repo head))
             (tree (libgit-commit-tree commit)))
        (should (string= "file1" (libgit-index-entry-path i1)))
        (should (string= "file2" (libgit-index-entry-path i2)))
        (should (string= "file3" (libgit-index-entry-path i3)))
        (should-not (libgit-index-entry-stage i1))
        (should-not (libgit-index-entry-stage i2))
        (should-not (libgit-index-entry-stage i3))
        (should (string= (libgit-index-entry-id i1)
                         (caddr (libgit-tree-entry-byname tree "file1"))))
        (should-not (string= (libgit-index-entry-id i2)
                             (caddr (libgit-tree-entry-byname tree "file2"))))))))

(ert-deftest index-conflicts ()
  (with-temp-dir path
    (init)
    (commit-change "file" "alpha")
    (create-branch "A")
    (commit-change "file" "beta")
    (checkout "master")
    (create-branch "B")
    (commit-change "file" "gamma")
    (run-fail "git" "merge" "A")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 3 (libgit-index-entrycount index)))
      (let ((i1 (libgit-index-get-byindex index 0))
            (i2 (libgit-index-get-byindex index 1))
            (i3 (libgit-index-get-byindex index 2)))
        (should (string= "file" (libgit-index-entry-path i1)))
        (should (string= "file" (libgit-index-entry-path i2)))
        (should (string= "file" (libgit-index-entry-path i3)))
        (should (eq 'base (libgit-index-entry-stage i1)))
        (should (eq 'ours (libgit-index-entry-stage i2)))
        (should (eq 'theirs (libgit-index-entry-stage i3)))
        (should (eq 'base (libgit-index-entry-stage (libgit-index-get-bypath index "file" 'base))))
        (should (eq 'ours (libgit-index-entry-stage (libgit-index-get-bypath index "file" 'ours))))
        (should (eq 'theirs (libgit-index-entry-stage (libgit-index-get-bypath index "file" 'theirs))))
        (should-not (libgit-index-get-bypath index "file"))
        (let ((conflicts (libgit-index-conflict-get index "file")))
          (should (eq 'base (libgit-index-entry-stage (car conflicts))))
          (should (eq 'ours (libgit-index-entry-stage (cadr conflicts))))
          (should (eq 'theirs (libgit-index-entry-stage (caddr conflicts)))))))))

(ert-deftest index-add-bypath ()
  (with-temp-dir path
    (init)
    (write "a" "abcdef")
    (write "b" "ghijkl")
    (write "c" "mnopqr")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 0 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file" (run "git" "status")))

      (libgit-index-add-bypath index "a")
      (should (= 1 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file" (run "git" "status")))
      (libgit-index-write index)
      (should (string-match-p "new file:\\s-+a$" (run "git" "status")))
      (should-not (string-match-p "new file:\\s-+b$" (run "git" "status")))

      (libgit-index-add-bypath index "b")
      (should (= 2 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file:\\s-+b$" (run "git" "status")))
      (libgit-index-write index)
      (should (string-match-p "new file:\\s-+b$" (run "git" "status")))
      (should-not (string-match-p "new file:\\s-+c$" (run "git" "status")))

      (libgit-index-clear index)
      (should (= 0 (libgit-index-entrycount index)))
      (should (string-match-p "new file:\\s-+b$" (run "git" "status")))
      (libgit-index-write index)
      (should-not (string-match-p "new file" (run "git" "status"))))))

(ert-deftest index-add-all ()
  (with-temp-dir path
    (init)
    (write "a.txt" "abcdef")
    (write "b.txt" "ghijkl")
    (write "subdir/c.txt" "mnopqr")
    (write "subdir/d" "stuvwx")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 0 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file" (run "git" "status")))

      (libgit-index-add-all index '("*.txt"))
      (should (= 3 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      ;; TODO: Maybe I don't understand what disable-pathspec-match should do?
      ;; (libgit-index-add-all index '("*.txt") '(disable-pathspec-match))
      ;; (should (= 0 (libgit-index-entrycount index)))
      ;; (libgit-index-clear index)

      (libgit-index-add-all index)
      (should (= 4 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      (libgit-index-add-all index '("z"))
      (should (= 0 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      (libgit-index-add-all index '("d"))
      (should (= 0 (libgit-index-entrycount index)))
      (libgit-index-clear index))))

(ert-deftest index-add-all-ignore ()
  (with-temp-dir path
    (init)
    (write "a.txt" "abcdef")
    (write "b.txt" "ghijkl")
    (write "subdir/c.txt" "mnopqr")
    (write "subdir/d" "stuvwx")
    (write ".git/info/exclude" "/a.txt\n/subdir/d")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 0 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file" (run "git" "status")))

      (libgit-index-add-all index '("*.txt"))
      (should (= 2 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      (libgit-index-add-all index '("*.txt") '(force))
      (should (= 3 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      (libgit-index-add-all index)
      (should (= 2 (libgit-index-entrycount index)))
      (libgit-index-clear index)

      (libgit-index-add-all index nil '(force))
      (should (= 4 (libgit-index-entrycount index))))))

(ert-deftest index-add-all-callback ()
  (with-temp-dir path
    (init)
    (write "a.txt" "abcdef")
    (write "b.txt" "ghijkl")
    (write "subdir/c.txt" "mnopqr")
    (write "subdir/d" "stuvwx")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo))
           data)
      (should (= 0 (libgit-index-entrycount index)))
      (should-not (string-match-p "new file" (run "git" "status")))

      (libgit-index-add-all index nil nil (lambda (&rest args) (push args data)))
      (setq data (sort data (lambda (a b) (string< (car a) (car b)))))
      (should (equal data
                     '(("a.txt" nil)
                       ("b.txt" nil)
                       ("subdir/c.txt" nil)
                       ("subdir/d" nil))))
      (libgit-index-clear index)
      (setq data nil)

      (libgit-index-add-all index '("*.txt") nil (lambda (&rest args) (push args data)))
      (setq data (sort data (lambda (a b) (string< (car a) (car b)))))
      (should (equal data
                     '(("a.txt" "*.txt")
                       ("b.txt" "*.txt")
                       ("subdir/c.txt" "*.txt")))))))

(ert-deftest index-read ()
  (with-temp-dir path
    (init)
    (write "a" "abcdef")
    (write "b" "ghijkl")
    (write "c" "mnopqr")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo)))
      (should (= 0 (libgit-index-entrycount index)))
      (run "git" "add" "a")
      (should (= 0 (libgit-index-entrycount index)))
      (libgit-index-read index)
      (should (= 1 (libgit-index-entrycount index)))
      (run "git" "add" "c")
      (should (= 1 (libgit-index-entrycount index)))
      (libgit-index-read index)
      (should (= 2 (libgit-index-entrycount index))))))

(ert-deftest index-write-tree ()
  (with-temp-dir path
    (init)
    (write "a" "abcdef")
    (write "b" "ghijkl")
    (write "subdir/c" "mnopqr")
    (let* ((repo (libgit-repository-open path))
           (index (libgit-repository-index repo))
           tree)
      (libgit-index-add-bypath index "a")
      (libgit-index-add-bypath index "b")
      (libgit-index-add-bypath index "subdir/c")
      (setq tree (libgit-tree-lookup repo (libgit-index-write-tree index)))
      (should (= 3 (libgit-tree-entrycount tree)))
      (should (libgit-tree-entry-bypath tree "a"))
      (should (libgit-tree-entry-bypath tree "b"))
      (should (libgit-tree-entry-bypath tree "subdir/c"))
      (should-error (libgit-tree-entry-bypath tree "notfound") :type 'giterr-tree))))
